;;;; -*- mode: Common-Lisp; sly-buffer-package: "yadfa-puzzle"; coding: utf-8-unix; -*-
(in-package :yadfa-puzzle)
(eval-when (:compile-toplevel :load-toplevel :execute)
  (define-presentation-type puzzle-piece () :inherit-from 'expression))
(defclass game-push-button (push-button) ())
(defvar *puzzle-size*)
(defvar *patterns*)
(defvar *puzzle*)
(defvar *swap-1*)
(defvar *swap-2*)
(defvar *win*)
(declaim (type list *swap-1* *swap-2* *puzzle-size*)
         (type array *puzzle* *patterns*)
         (type (or boolean cons) *win*))
(define-command-table puzzle-commands)
(define-conditional-application-frame game-frame
    ()
  (:enable-commands (puzzle-commands))
  ()
  (:command-table (game-frame :inherit-from (puzzle-commands)))
  (:pane (vertically ()
             (make-clim-stream-pane :name 'puzzle :incremental-redisplay t :scroll-bars nil
                                  :display-time :command-loop :display-function 'draw-puzzle :width 800 :height 450)
             (make-clim-interactor-pane :name 'int :display-time :command-loop :width 800 :height 150))))
(define-game-frame-command (com-exit-game :name t)
    ()
  (frame-exit *application-frame*))
(defmethod run-frame-top-level ((frame game-frame) &key)
  (let ((*patterns* (make-array `(,(cdr *puzzle-size*) ,(cdr *puzzle-size*))
                                :initial-contents (iter
                                                    (with pw = (car *puzzle-size*))
                                                    (with ps = (cdr *puzzle-size*))
                                                    (with pattern = (mcclim-raster-image:with-output-to-rgba-pattern
                                                                        (stream :width pw :height pw)
                                                                      (draw-rectangle* stream 0 0 pw pw :ink +green+)
                                                                      (flet ((draw-rosette2 (stream x y radius n &rest drawing-options)
                                                                               (loop with alpha = (/ (* 2 pi) n)
                                                                                     and radius = (/ radius 2)
                                                                                     for i below n
                                                                                     do (apply #'clim:draw-circle* stream
                                                                                               (+ (* radius (cos (* alpha i))) x)
                                                                                               (+ (* radius (sin (* alpha i))) y)
                                                                                               radius
                                                                                               :filled nil
                                                                                               drawing-options))))
                                                                        (draw-rosette2 stream (/ pw 2) (/ pw 2) (/ pw 2) 18
                                                                                       :ink clim:+steel-blue+ :line-thickness 2))))
                                                    (for y from 0 to (1- ps))
                                                    (collect (iter (for x from 0 to (1- ps))
                                                               (collect (make-rectangular-tile
                                                                         (transform-region (make-translation-transformation (* x (- (/ pw ps)))
                                                                                                                            (* y (- (/ pw ps))))
                                                                                           pattern)
                                                                         (/ pw ps) (/ pw ps))))))))
        (*puzzle* (make-array `(,(cdr *puzzle-size*) ,(cdr *puzzle-size*))
                              :element-type 'cons
                              :initial-contents (iter (with a = (make-array (* (cdr *puzzle-size*) (cdr *puzzle-size*))
                                                                            :element-type 'cons
                                                                            :initial-contents (alexandria:shuffle
                                                                                               (iter
                                                                                                 (for y from 0 to (1- (cdr *puzzle-size*)))
                                                                                                 (with i = 0)
                                                                                                 (appending (iter (for x from 0 to (1- (cdr *puzzle-size*)))
                                                                                                              (collect `(,i ,y ,x))
                                                                                                              (incf i)))))))
                                                  (with i = 0)
                                                  (for y from 0 to (1- (cdr *puzzle-size*)))
                                                  (collect (iter (for x from 0 to (1- (cdr *puzzle-size*)))
                                                             (collect (aref a i))
                                                             (incf i))))))
        (*swap-1* nil)
        (*swap-2* nil)
        *win*)
    (declare (special *patterns* *puzzle* *swap-1* *swap-2* *win*)
             (type list *swap-1* *swap-2* *puzzle-size*)
             (type array *puzzle* *patterns*)
             (type (or boolean cons) *win*))
    (handler-case (call-next-method)
      (frame-exit ()
        *win*))))
(define-conditional-command (com-end-puzzle :name t)
    (game-frame :disable-commands (puzzle-commands))
    ())
(define-command (com-select-piece :name t :command-table puzzle-commands)
    ((piece puzzle-piece :prompt "Which Piece?"))
  (locally (declare (type list piece))
    (setf *swap-2* *swap-1*
          *swap-1* piece)))
(define-command (com-swap-pieces :name t :command-table puzzle-commands)
    ()
  (when (and *swap-1* *swap-2*)
    (let ((swap (apply #'aref *puzzle* *swap-1*)))
      (declare (type cons swap))
      (setf (apply #'aref *puzzle* *swap-1*) (apply #'aref *puzzle* *swap-2*)
            (apply #'aref *puzzle* *swap-2*) swap
            *swap-1* nil
            *swap-2* nil))
    (let ((had-accident (process-potty)))
      (declare (type cons had-accident))
      (cond ((apply '< (iter (for y from 0 to (1- (cdr *puzzle-size*)))
                         (appending (iter (for x from 0 to (1- (cdr *puzzle-size*)))
                                      (collect (car (aref *puzzle* y x)))))))
             (setf *win* t)
             (change-entity-enabledness 'com-end-puzzle)
             (write-line "You completed the puzzle"))
            ((or (car had-accident) (cdr had-accident))
             (setf *win* had-accident)
             (change-entity-enabledness 'com-end-puzzle)
             (format t
                     "You ［箕狺箕犷狺夯利狺［滑犷夯蔺蔺秕疳眇簋アㄣ镬戾泗轭绐麒孱ㄣ狎栳洵徙汩溴铘ㄣ镬戾泗㈡祜镤邃┅麒孱ㄣ潋栳洵徙汩溴铘ㄣ镬戾泗㈨弩箦洧┅┅┅┅ㄤ彐轭瀛痱弩孱翎糸镱麸泔眄犷洵趄犷箪狒矧箦戾泗痖邈瘐戾痖邈泔憝箦戾泗痖邈玑礤骝犴虹弩趱蝈后屐邈轰镢蹴孱翎糸镱⒂屐邈虚邈澧吼镩铘弪滹沲礤铘狒轱⒂屐邈虚邈澧镡赍泗扉篝镡赍泗┅ㄤ彐躅潋狩瘐戾ㄦ蜥礤疳铄戾舄è瘅ㄣ狎瘐戾箝濯┅痼ㄣ潋瘐戾箝濯┅瘌ǒ瘅痼┅ㄩ翦ㄦ矧骝镯麸ū痼┅ㄩ翦ㄦ矧骝镯麸ū痼┅躔溽糸铉秕麴豸疳铄乎铋聃瀛殇啜洪洵翦篝у聃犰恒徙桢鲠祯篪栳箬啜ㄡ蝈瘐戾麒孱ㄥ聃犰篦狃豹啜┅啜┅麒孱ㄥ聃犰篦狃勃啜┅啜┅┅鏖翳秕麴豸狍痱弩孱翎糸镱疳铄啜ю斛戾痖邈濠ㄤ蜥鳝溴箝珙疳铄ㄡ痧禊＇狎彐疳趑弪铙ㄣ潋ㄡ蝈瘐戾┅呼蜥铙骘蝽狒轱戾è趄犷箪狒磲脲趄犷箪狒轱瞽趄犷箧矧磲糸镱í瘌í瘌┅┅ㄩ矧ㄥ聃犰篦狃豹啜┅ㄥ聃犰篦狃勃啜┅ㄣ镯痫箦趄犷箧矧磲糸镱趄犷箪狒磲脲筱犰轭绛趄犷箧矧磲糸镱爱爱磲脲痫轭ǒ瘌博ǒ瘌博┅趄犷箪狒濠┅┅┅箦翩篝蝈犴沲蝮矧痫箝糸镱疳铄鲠祯弩舶瘅┅ㄦ矧磲趑轭绛轸屙扉篝疳铄躔溽糸铉秕麴豸疳铄恒徙桢鲠祯鏖瞠豉疱汜箦鏖瞠铛祆ㄦ矧磲趑轭绛沐祆疳铄鏖翳秕麴豸狍痱弩孱翎糸镱疳铄Ж泔憝篦狃痖邈弩啜泔眄犷恒镯磲钿翎忪ㄦ蜥礤泔眄犷洵翎忪骝犴濠┅篚蝌秕钿轭绛秕麴豸鏖翳怙蜾弪疳铄后栳疱候秕钿邃候徜轷衡徙腌蝻躅珧狴赴鸿殓桁殓梏忉汶珧秕钿珧狴拱ㄦ矧磲疳铄⒂麽虚邈弩┅┅ㄦ矧磲趑轭绛沐祆疳铄鏖翳秕麴豸狍痱弩孱翎糸镱疳铄Ж泔憝屮轸玑礤啜泔眄犷恒镯磲钿翎忪ㄦ蜥礤泔眄犷洵翎忪骝犴濠┅篚蝌秕钿轭绛秕麴豸鏖翳怙蜾弪疳铄后栳疱候秕钿邃候徜轷衡徙腌蝻躅珧狴赴鸿殓桁殓梏忉汶珧秕钿珧狴拱ㄦ矧磲疳铄⑴轸轻礤┅┅┅ㄦ矧磲趑轭绛沐祆疳铄痱弩孱痨狴弪镦玑礤豉疱镦痨狴弪镦玑礤┅忽殄篝狒鲩鬻后趄遽疳铄┅┅ㄤ彐躅蝓瞽玑礤é脲鏖漪巢癌ㄨ彘玷旦戾è瘐戾箝濯啜鏖漪桢殓梏┅蝓瞽骝犴瀛麸瓠戾鲥磲脲狃痨殂狒轱瞽骝犴х犴瀛骝犴吼蝈趑钺礤⑿斛戾瑚殇翳赴鸿彘玷栋癌┅